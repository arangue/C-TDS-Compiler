package main.java.parser;

import java_cup.runtime.*;
import main.java.ast.*;
import java.util.LinkedList;
import java.util.List;
import java.io.*;
import java.util.*;

parser code
{:

public void syntax_error(Symbol s){ 
            System.out.println("Syntax error at line " + (s.left+1) + " column " + s.right+ " token "+(String)s.value); 
    }

public void unrecovered_syntax_error(Symbol s){
        System.out.println("There was an unrecovered error at line "+(s.left+1)+" column " + s.right+" with symbol "+(String)s.value);
}

:}

/*Terminals*/

terminal TBOOL;
terminal TINTEGER;
terminal TFLOAT;
terminal TVOID;

terminal BREAK;
terminal CLASS;
terminal CONTINUE;
terminal ELSE;
terminal FOR;
terminal IF;
terminal RETURN;
terminal WHILE;
terminal EXTERN;
terminal LPAREN;
terminal RPAREN;
terminal LBRACE;
terminal RBRACE;
terminal LBRACK;
terminal RBRACK;

terminal SEMICOLON;
terminal COMMA;
terminal GT;
terminal LT;
terminal NOT;
terminal EQEQ;
terminal LTEQ;
terminal GTEQ;
terminal NOTEQ;
terminal ANDAND;
terminal OROR;
terminal PLUS;
terminal MINUS;
terminal UMINUS;
terminal MOD;
terminal ASSIGN;
terminal INC;
terminal DEC;
terminal DOT;

terminal DIV;
terminal MULT;
terminal    Integer INT_LITERAL;
terminal    Float FLOAT_LITERAL;

terminal    Boolean TRUE;
terminal    Boolean FALSE;

terminal    String ID;

/*Non-Terminals*/

non terminal Program program; 
non terminal ClassDecl class_decl;
non terminal BodyClass body_class;
non terminal List<ClassDecl> class_list;
non terminal MethodDecl method_decl;
non terminal List<MethodDecl> method_list;
non terminal IdFieldDecl id_field_decl;
non terminal List<IdFieldDecl> list_id_field_decl;
non terminal Param param; 
non terminal List<Param> param_list;
non terminal Block body;
non terminal AssignOpType assign_op;
non terminal MethodCall method_call;
non terminal Block block;
non terminal Type type;
non terminal Expression expr;
non terminal List<Expression> list_expr;
non terminal Location location;
non terminal List<String> list_invoc;
non terminal String invoc;
non terminal Literal literal;

non terminal Boolean bool_literal;

non terminal FieldDecl field_decl;
non terminal List<FieldDecl> field_list;
non terminal Statement statement, if_stmt, return_stmt;
non terminal List<Statement> list_statement;



/*Precedences*/
precedence left  ANDAND;
precedence left  OROR;
precedence nonassoc  LT, LTEQ, GTEQ, GT, EQEQ, NOTEQ;
precedence left  PLUS, MINUS;
precedence left  MULT, DIV, MOD;
precedence right NOT, UMINUS;


start with program;


program				::= class_list:cl										{: RESULT = new Program(cl, clleft+1, clright+1); :}
					;
																			//{: RESULT = new LinkedList<ClassDecl>(); RESULT.add(cd); :}
class_list			::=  class_decl:cd										{: LinkedList res = new LinkedList<ClassDecl>(); res.add(cd); RESULT = res ; :}
					|class_list:cl class_decl:cd							{: cl.add(cd); RESULT = cl; :}
					;

class_decl			::=  CLASS ID:id body_class:bc							{: RESULT = new ClassDecl(id, bc, idleft+1, idright+1); :}
					;

body_class			::= LBRACE RBRACE								
					|LBRACE field_list:fl RBRACE							{: RESULT = new BodyClass(fl, new LinkedList<MethodDecl>(), flleft+1, flright+1); :}
					|LBRACE method_list:ml RBRACE							{: RESULT = new BodyClass(new LinkedList<FieldDecl>(), ml, mlleft+1, mlright+1); :}
					|LBRACE field_list:fl method_list:ml RBRACE				{: RESULT = new BodyClass(fl, ml, flleft+1, flright+1); :}
					;
/****************************************************Field**************************************************************/

/*Field Declaration*/


/**List Field Decl*/
																			//{: RESULT = new LinkedList<FieldDecl>(); RESULT.add(fd); :}
field_list			::= field_decl:fd										{: LinkedList res = new LinkedList<FieldDecl>(); res.add(fd); RESULT = res; :}
					|field_list:fl field_decl:fd							{: fl.add(fd); RESULT = fl; :}
					;

/*Field Decl*/
field_decl			::= type:t list_id_field_decl:lifd SEMICOLON			{: RESULT = new FieldDecl(t, lifd); :}
					;

/*String*/					//##### ACA DEJE TAL CUAL TENIAMOS PERO MODIFIQUE LA CLASE IdFieldDecl #####
id_field_decl		::= ID:id												{: RESULT = new IdFieldDecl(id); :}
					| ID:id LBRACK INT_LITERAL:il RBRACK					{: RESULT = new IdFieldDecl(id, il); :}
					;

/*List String*/
list_id_field_decl	::= list_id_field_decl:lifd COMMA id_field_decl:ifd		{: lifd.add(ifd); RESULT = lifd; :}
					| id_field_decl:ifd										{: LinkedList res = new LinkedList<IdFieldDecl>(); res.add(ifd); RESULT = res; :}
					;														//{: RESULT = new LinkedList<IdFieldDecl>(); RESULT.add(ifd); :}

/****************************************************EndField**************************************************************/

/*Method Declaration*/
method_list			::= method_decl:md										{: LinkedList res = new LinkedList<MethodDecl>(); res.add(md); RESULT = res; :}
																			//{: RESULT = new LinkedList<MethodDecl>(); RESULT.add(md); :}
					|method_list:ml method_decl:md							{: ml.add(md); RESULT = ml; :}
					;

method_decl			::= type:t ID:id LPAREN RPAREN body:b					{: RESULT = new MethodDecl(t, id, new LinkedList<Param>(), b, idleft+1, idright+1); :}
					|type:t ID:id LPAREN param_list:pl RPAREN body:b		{: RESULT = new MethodDecl(t, id, pl, b, idleft+1, idright+1); :}
					;

/*************************************Parameter****************************************************************/
																			//{: RESULT = new LinkedList<Param>(); RESULT.add(p); :}
param_list			::= param:p												{: LinkedList res = new LinkedList<Param>(); res.add(p); RESULT = res; :}
					| param_list:pl COMMA param:p							{: pl.add(p); RESULT = pl; :}
					;

param				::= type:t ID:id										{: RESULT = new Param(t, id, idleft+1, idright+1); :}
					;
/*************************************EndParameter***********************************************************/

body				::= block:b												{: RESULT = b; :}
					| EXTERN SEMICOLON										{: RESULT = null; :}
					;

block				::= LBRACE RBRACE 										{: RESULT = new Block(); :}
					| LBRACE field_list:fl list_statement:ls RBRACE         {: RESULT = new Block(fl,ls); :}
					| LBRACE field_list:fl RBRACE							{: RESULT = new Block(fl,new LinkedList<Statement>()); :}
					| LBRACE list_statement:ls RBRACE						{: RESULT = new Block(new LinkedList<FieldDecl>(),ls); :}
					;

type				::= TINTEGER      	{:RESULT = Type.TINTEGER;:}
					| TBOOL	 	   		{:RESULT = Type.TBOOL;:}
					| TFLOAT		   	{:RESULT = Type.TFLOAT;:}
					| TVOID		   		{:RESULT = Type.TVOID;:}
					;
	   
/********************************************Statement*************************************************************/

statement			::= location:l assign_op:op expr:e SEMICOLON 		{: RESULT = new AssignStmt(l,op,e,lleft+1,lright+1);:}
					| method_call:mc SEMICOLON 							{: RESULT = new MethodCallStmt(mc,mcleft+1,mcright+1);:}
					| if_stmt:ifs 										{: RESULT = ifs; :}						
					| FOR ID:id ASSIGN expr:value COMMA expr:e block:b 	{: RESULT = new ForStatement(id,value,e,b,idleft+1,idright+1) ;:}
					| WHILE expr:e block:bl 							{: RESULT = new WhileStatement(e,bl, eleft+1, eright+1); :}
					| return_stmt:rts 									{: RESULT = rts; :}
					| BREAK:b SEMICOLON 								{: RESULT = new BreakStatement(bleft+1,bright+1); :}
					| CONTINUE:c SEMICOLON 								{: RESULT = new ContinueStmt(cleft+1,cright+1); :}
					| SEMICOLON:s 										{: RESULT = new SemicolonStmt(sleft+1,sright+1); :}
					| block:b 											{: RESULT = b; :}
					;

if_stmt				::= IF LPAREN expr:e RPAREN block:b 				 		{:RESULT = new IfStatement(e,b,bleft+1,bright+1);:}
					| IF LPAREN expr:e RPAREN block:b1 ELSE block:b2 			{:RESULT = new IfStatement(e,b1,b2,b1left+1,b2right+1);:}
					; 

return_stmt			::= RETURN:r SEMICOLON 										{: RESULT = new ReturnStmt(rleft+1,rright+1); :}
					| RETURN:r expr:e SEMICOLON 								{: RESULT = new ReturnStmt(e,rleft+1,rright+1); :}
					;			

list_statement		::= list_statement:sl statement:s  					{:sl.add(s); RESULT = sl;:}
					| statement:s 										{:LinkedList l = new LinkedList<Statement>(); l.add(s); RESULT = l;:}					;
/*******************************************EndStatement*********************************************************/

assign_op			::= ASSIGN 		{:RESULT = AssignOpType.ASSIGN;:}
					| INC 			{:RESULT = AssignOpType.INC;:}
					| DEC 			{:RESULT = AssignOpType.DEC;:}
					;

method_call			::= ID:id LPAREN RPAREN 							{:RESULT = new MethodCall(id,idleft+1,idright+1);:}
					| ID:id LPAREN list_expr:le RPAREN					{:RESULT = new MethodCall(id,new LinkedList<String>(),(List)le,idleft+1,idright+1);:}
					| ID:id list_invoc:li LPAREN list_expr:le RPAREN 	{:RESULT = new MethodCall(id,li,(List)le,idleft+1,idright+1);:}
					| ID:id list_invoc:li LPAREN RPAREN 				{:RESULT = new MethodCall(id,li,idleft+1,idright+1);:}
					;



/***************************************Expression************************************************/

expr ::= location:loc {: RESULT = loc; :}
	   |method_call:c {: RESULT = c; :}
	   |literal:lit {: RESULT = lit; :}

	   |expr:e1 PLUS:plu expr:e2  {: RESULT = new BinOpExpr(e1,BinOpType.PLUS,e2,pluleft+1,pluright+1); :}

	   |expr:e1 MINUS:min expr:e2 {: RESULT = new BinOpExpr(e1,BinOpType.MINUS,e2,minleft+1,minright+1); :}

	   |expr:e1 MULT:mul expr:e2 {: RESULT = new BinOpExpr(e1,BinOpType.MULT,e2,mulleft+1,mulright+1); :}

	   |expr:e1 DIV:di expr:e2 {: RESULT = new BinOpExpr(e1,BinOpType.DIV,e2,dileft+1,diright+1); :}

	   |expr:e1 MOD:mo expr:e2 {: RESULT = new BinOpExpr(e1,BinOpType.MOD,e2,moleft+1,moright+1); :}

	   |expr:e1 LT:lt expr:e2 {: RESULT = new BinOpExpr(e1,BinOpType.LT,e2,ltleft+1,ltright+1); :}

	   |expr:e1 GT:gt expr:e2 {: RESULT = new BinOpExpr(e1,BinOpType.GT,e2,gtleft+1,gtright+1); :}

	   |expr:e1 LTEQ:lteq expr:e2 {: RESULT = new BinOpExpr(e1,BinOpType.LTEQ,e2,lteqleft+1,lteqright+1); :}

	   |expr:e1 GTEQ:gteq expr:e2 {: RESULT = new BinOpExpr(e1,BinOpType.GTEQ,e2,gteqleft+1,gteqright+1); :}

	   |expr:e1 EQEQ:eqeq expr:e2 {: RESULT = new BinOpExpr(e1,BinOpType.EQEQ,e2,eqeqleft+1,eqeqright+1); :}

	   |expr:e1 NOTEQ:noteq expr:e2 {: RESULT = new BinOpExpr(e1,BinOpType.NOTEQ,e2,noteqleft+1,noteqright+1); :}

	   |expr:e1 ANDAND:andand expr:e2 {: RESULT = new BinOpExpr(e1,BinOpType.ANDAND,e2,andandleft+1,andandright+1); :}

	   |expr:e1 OROR:oror expr:e2 {: RESULT = new BinOpExpr(e1,BinOpType.OROR,e2,ororleft+1,ororright+1); :}

	   |MINUS:minU expr:e1 {: RESULT = new UnaryOpExpr(UnaryOpType.MINUS,e1,minUleft+1,minUright+1); :}%prec UMINUS

	   |NOT:not expr:e1 {: RESULT = new UnaryOpExpr(UnaryOpType.NOT,e1,notleft+1,notright+1); :}

	   |LPAREN expr:e1 RPAREN {: RESULT = e1; :}
	   ;



list_expr ::= expr:e1 {: RESULT = new LinkedList<Expression>(Arrays.asList(e1)); :}
			| expr:e1 COMMA list_expr:l {: l.add(0,e1); RESULT = (List<Expression>)l;:}
			;

/***************************************EndExpression***********************************************/

list_invoc			::= invoc:i												{:LinkedList res = new LinkedList(); res.add(i); RESULT = res;:}
					|list_invoc:li invoc:i									{:li.add(i); RESULT = li;:}
					;

invoc				::= DOT ID:id											{:RESULT = id;:}
					; 

location			::= ID:id {: RESULT = new VarLocation(id,idleft+1,idright+1); :}
					|ID:id list_invoc:l {: RESULT = new VarLocation(id,l,idleft+1,idright+1); :}
					|ID:id LBRACK expr:e RBRACK {: RESULT = new VarListLocation(id,e,idleft+1,idright+1); :}
					|ID:id list_invoc:l LBRACK expr:e RBRACK {: RESULT = new VarListLocation(id,e,l,idleft+1,idright+1); :}
					;

																				//#### CREO UN INTLITERAL ####
literal				::= INT_LITERAL:l										{:RESULT = new IntLiteral(l);:}
					|FLOAT_LITERAL:l										{:RESULT = new FloatLiteral(l);:}
					|bool_literal:l											{:RESULT = new BoolLiteral(l);:}
					;

bool_literal		::= TRUE:t 												{: RESULT = t; :}
					| FALSE:f 												{: RESULT = f; :}
					;

